/* ix87 specific frexp implementation for double.
   Copyright (C) 1997, 2000, 2005 Free Software Foundation, Inc.
   This file is part of the GNU C Library.
   Contributed by Ulrich Drepper <drepper@cygnus.com>, 1997.

   The GNU C Library is free software; you can redistribute it and/or
   modify it under the terms of the GNU Lesser General Public
   License as published by the Free Software Foundation; either
   version 2.1 of the License, or (at your option) any later version.

   The GNU C Library is distributed in the hope that it will be useful,
   but WITHOUT ANY WARRANTY; without even the implied warranty of
   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
   Lesser General Public License for more details.

   You should have received a copy of the GNU Lesser General Public
   License along with the GNU C Library; if not, write to the Free
   Software Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA
   02111-1307 USA.  */


  .file "frexp.s"
	.text

.align 4
two54:	.byte 0, 0, 0, 0, 0, 0, 0x50, 0x43

	.p2align 4,,15

.globl _frexp
.def   _frexp;  .scl  2;  .type 32; .endef

_frexp:

      movl  4(%esp), %ecx
      movl  8(%esp), %eax
      movl  %eax, %edx
      andl  $0x7fffffff, %eax
      orl %eax, %ecx
      jz  1f

      xorl  %ecx, %ecx
      cmpl  $0x7ff00000, %eax
      jae 1f

      cmpl  $0x00100000, %eax
      jae 2f

      fld  4(%esp)

      fmull two54
      movl  $-54, %ecx
      fstpl 4(%esp)
      fwait
      movl  8(%esp), %eax
      movl  %eax, %edx
      andl  $0x7fffffff, %eax

2:
      shrl  $20, %eax
      andl  $0x800fffff, %edx
      subl  $1022, %eax
      orl $0x3fe00000, %edx
      addl  %eax, %ecx
      movl  %edx, 8(%esp)

	/* Store %ecx in the variable pointed to by the second argument,
	   get the factor from the stack and return.  */
1:
      movl  12(%esp), %eax
      fldl  4(%esp)
      movl  %ecx, (%eax)

      ret
