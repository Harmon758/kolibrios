/*
 * Copyright Â© 2009 Intel Corporation
 *
 * Permission is hereby granted, free of charge, to any person obtaining a
 * copy of this software and associated documentation files (the "Software"),
 * to deal in the Software without restriction, including without limitation
 * the rights to use, copy, modify, merge, publish, distribute, sublicense,
 * and/or sell copies of the Software, and to permit persons to whom the
 * Software is furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice (including the next
 * paragraph) shall be included in all copies or substantial portions of the
 * Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.  IN NO EVENT SHALL
 * THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 *
 * Author:
 *    Zou Nan hai <nanhai.zou@intel.com>
 *    Yan Li <li.l.yan@intel.com>
 *    Liu Xi bin<xibin.liu@intel.com>
 */
/* GRF allocation:
   g1~g30: constant buffer
           g1~g2:intra IQ matrix
           g3~g4:non intra IQ matrix
           g5~g20:IDCT table
   g31:    thread payload 
   g58~g81:reference data
   g82:    thread payload backup
   g83~g106:IDCT data
   g32:   message descriptor for reading reference data   */

mov (8) g82.0<1>UD g31.0<8,8,1>UD {align1};
mov (2) g31.0<1>UD g82.12<2,2,1>UW {align1};
mov (1) g126.8<1>UD ip {align1};
mov (1) ip g21.0<1,1,1>UD {align1};

//Y of forward 
asr (2) g31.14<1>W g82.16<2,2,1>W 1W {align1};
add (2) g32.0<1>UD g31.0<2,2,1>UD g31.14<2,2,1>W {align1};
mov (1) g32.28<1>UD 0UD {align1};
and.nz (1) null g82.2<1,1,1>UW 0x1000UW {align1};             //motion vertical field select
(f0) mov (1) g32.28<1>UD 1UD {align1};
shl (1) g32.4<1>UD g32.4<1,1,1>UD 1UD {align1};
add (1) g32.4<1>UD g32.4<1,1,1>UD g32.28<1,1,1>UD {align1};
define(`surface',`4')
define(`mv1',`g82.16')
define(`mv2',`g82.18')
include(`field_motion_y.g4i')
mov (16) g108.0<1>UD g58.0<16,16,1>UD {align1 compr};
mov (16) g110.0<1>UD g60.0<16,16,1>UD {align1 compr};
mov (16) g112.0<1>UD g62.0<16,16,1>UD {align1 compr};
mov (16) g114.0<1>UD g64.0<16,16,1>UD {align1 compr};
mov (16) g116.0<1>UD g66.0<16,16,1>UD {align1 compr};
mov (16) g118.0<1>UD g68.0<16,16,1>UD {align1 compr};
mov (16) g120.0<1>UD g70.0<16,16,1>UD {align1 compr};
mov (16) g122.0<1>UD g72.0<16,16,1>UD {align1 compr};

//Y of backward 
asr (2) g31.14<1>W g82.20<2,2,1>W 1W {align1};
add (2) g32.0<1>UD g31.0<2,2,1>UD g31.14<2,2,1>W {align1};
mov (1) g32.28<1>UD 0UD {align1};
and.nz (1) null g82.2<1,1,1>UW 0x2000UW {align1};             //motion vertical field select
(f0) mov (1) g32.28<1>UD 1UD {align1};
shl (1) g32.4<1>UD g32.4<1,1,1>UD 1UD {align1};
add (1) g32.4<1>UD g32.4<1,1,1>UD g32.28<1,1,1>UD {align1};
define(`surface',`7')
define(`mv1',`g82.20')
define(`mv2',`g82.22')
include(`field_motion_y.g4i')
avg (16) g58.0<1>UW g58.0<16,16,1>UW g108.0<16,16,1>UW {align1};
avg (16) g59.0<1>UW g59.0<16,16,1>UW g109.0<16,16,1>UW {align1};
avg (16) g60.0<1>UW g60.0<16,16,1>UW g110.0<16,16,1>UW {align1};
avg (16) g61.0<1>UW g61.0<16,16,1>UW g111.0<16,16,1>UW {align1};
avg (16) g62.0<1>UW g62.0<16,16,1>UW g112.0<16,16,1>UW {align1};
avg (16) g63.0<1>UW g63.0<16,16,1>UW g113.0<16,16,1>UW {align1};
avg (16) g64.0<1>UW g64.0<16,16,1>UW g114.0<16,16,1>UW {align1};
avg (16) g65.0<1>UW g65.0<16,16,1>UW g115.0<16,16,1>UW {align1};
avg (16) g66.0<1>UW g66.0<16,16,1>UW g116.0<16,16,1>UW {align1};
avg (16) g67.0<1>UW g67.0<16,16,1>UW g117.0<16,16,1>UW {align1};
avg (16) g68.0<1>UW g68.0<16,16,1>UW g118.0<16,16,1>UW {align1};
avg (16) g69.0<1>UW g69.0<16,16,1>UW g119.0<16,16,1>UW {align1};
avg (16) g70.0<1>UW g70.0<16,16,1>UW g120.0<16,16,1>UW {align1};
avg (16) g71.0<1>UW g71.0<16,16,1>UW g121.0<16,16,1>UW {align1};
avg (16) g72.0<1>UW g72.0<16,16,1>UW g122.0<16,16,1>UW {align1};
avg (16) g73.0<1>UW g73.0<16,16,1>UW g123.0<16,16,1>UW {align1};

//UV, Forward
shr (2) g31.0<1>UD g31.0<2,2,1>UD 1UD {align1};
asr (2) g31.14<1>W g82.16<2,2,1>W 2W {align1};
add (2) g32.0<1>UD g31.0<2,2,1>UD g31.14<2,2,1>W {align1};
mov (1) g32.28<1>UD 0UD {align1};
and.nz (1) null g82.2<1,1,1>UW 0x1000UW {align1};             //motion vertical field select
(f0) mov (1) g32.28<1>UD 1UD {align1};
shl (1) g32.4<1>UD g32.4<1,1,1>UD 1UD {align1};
add (1) g32.4<1>UD g32.4<1,1,1>UD g32.28<1,1,1>UD {align1};
define(`surface_u', `5')
define(`surface_v', `6')
define(`mv1',`g82.16')
define(`mv2',`g82.18')
include(`field_motion_uv.g4i')
//Save UV Forward
mov (16) g108.0<1>UD g74.0<16,16,1>UD {align1 compr};
mov (16) g110.0<1>UD g76.0<16,16,1>UD {align1 compr};
mov (16) g112.0<1>UD g78.0<16,16,1>UD {align1 compr};
mov (16) g114.0<1>UD g80.0<16,16,1>UD {align1 compr};

//UV, Backward
asr (2) g31.14<1>W g82.20<2,2,1>W 2W {align1};
add (2) g32.0<1>UD g31.0<2,2,1>UD g31.14<2,2,1>W {align1};
mov (1) g32.28<1>UD 0UD {align1};
and.nz (1) null g82.2<1,1,1>UW 0x2000UW {align1};             //motion vertical field select
(f0) mov (1) g32.28<1>UD 1UD {align1};
shl (1) g32.4<1>UD g32.4<1,1,1>UD 1UD {align1};
add (1) g32.4<1>UD g32.4<1,1,1>UD g32.28<1,1,1>UD {align1};
define(`surface_u', `8')
define(`surface_v', `9')
define(`mv1',`g82.20')
define(`mv2',`g82.22')
include(`field_motion_uv.g4i')
//Average Forward and Backward
avg (16) g74.0<1>UW g74.0<16,16,1>UW g108.0<16,16,1>UW {align1};
avg (16) g75.0<1>UW g75.0<16,16,1>UW g109.0<16,16,1>UW {align1};
avg (16) g76.0<1>UW g76.0<16,16,1>UW g110.0<16,16,1>UW {align1};
avg (16) g77.0<1>UW g77.0<16,16,1>UW g111.0<16,16,1>UW {align1};
avg (16) g78.0<1>UW g78.0<16,16,1>UW g112.0<16,16,1>UW {align1};
avg (16) g79.0<1>UW g79.0<16,16,1>UW g113.0<16,16,1>UW {align1};
avg (16) g80.0<1>UW g80.0<16,16,1>UW g114.0<16,16,1>UW {align1};
avg (16) g81.0<1>UW g81.0<16,16,1>UW g115.0<16,16,1>UW {align1};

field_addidct:
include(`field_addidct.g4i')
out:
send (16) 0 acc0<1>UW g0<8,8,1>UW 
	thread_spawner(0, 0, 0) mlen 1 rlen 0 { align1 EOT};
